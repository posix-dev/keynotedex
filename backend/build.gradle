group = 'es.guillermoorellana.keynotedex'
version = '0.0.1-SNAPSHOT'

buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "com.github.jengelman.gradle.plugins:shadow:2.0.1"
    }
}

apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: "com.github.johnrengelman.shadow"

mainClassName = "es.guillermoorellana.keynotedex.backend.ServerKt"

dependencies {
    compile project(":common-jvm")

    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    compile "io.ktor:ktor-locations:$ktor_version"
    compile "io.ktor:ktor-gson:$ktor_version"
    compile "io.ktor:ktor-html-builder:$ktor_version"
    compile "io.ktor:ktor-server-netty:$ktor_version"
    compile "org.ehcache:ehcache:3.0.0.m4"

    compile "org.jetbrains.squash:squash-h2:$squash_version"

    testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    testCompile "io.ktor:ktor-server-test-host:$ktor_version"
    testCompile "org.jsoup:jsoup:1.9.1"

    compile "com.google.code.gson:gson:2.8.0"
    compile "org.slf4j:slf4j-simple:1.7.25"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

kotlin {
    experimental {
        coroutines "enable"
    }
}

task release() {
    dependsOn('clean')
    dependsOn('shadowJar')
}

shadowJar.mustRunAfter clean

task includeWebBundle(type: Copy) {
    dependsOn(':web:build')
    from('../web/build/bundle')
    into('build/resources/main')
}

shadowJar.dependsOn includeWebBundle
