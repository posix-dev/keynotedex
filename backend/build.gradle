import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

group = 'es.guillermoorellana.keynotedex'
version = '0.0.1-SNAPSHOT'

buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "com.github.jengelman.gradle.plugins:shadow:2.0.2"
    }
}

apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: "com.github.johnrengelman.shadow"

mainClassName = "es.guillermoorellana.keynotedex.backend.ServerKt"

sourceSets {
    release {
        kotlin {
            srcDirs += sourceSets.main.kotlin.srcDirs
        }
        resources {
            srcDirs += sourceSets.main.resources.srcDirs
        }

        compileClasspath += sourceSets.main.compileClasspath
        runtimeClasspath += sourceSets.main.runtimeClasspath
    }
}

configurations {
    releaseCompile.extendsFrom compile
}

dependencies {
    compile project(":common-jvm")

    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    compile "io.ktor:ktor-locations:$ktor_version"
    compile "io.ktor:ktor-gson:$ktor_version"
    compile "io.ktor:ktor-html-builder:$ktor_version"
    compile "io.ktor:ktor-server-netty:$ktor_version"
    compile "org.ehcache:ehcache:3.0.0.m4"

    compile "org.jetbrains.squash:squash-h2:$squash_version"

    testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    testCompile "io.ktor:ktor-server-test-host:$ktor_version"
    testCompile "org.jsoup:jsoup:1.9.1"

    compile "com.google.code.gson:gson:2.8.0"
    compile "org.slf4j:slf4j-simple:1.7.25"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

kotlin {
    experimental {
        coroutines "enable"
    }
}

task releaseJar(type: ShadowJar) {
    manifest {
        attributes "Main-Class": "es.guillermoorellana.keynotedex.backend.ServerKt"
    }
    classifier = 'release'
    from([sourceSets.release.output, sourceSets.main.output])
    configurations = [project.configurations.compile]
}

task release() {
    dependsOn('clean')
    dependsOn('releaseJar')
}

releaseJar.mustRunAfter clean

task copyWebBundle(type: Copy) {
    dependsOn(':web:build')
    from('../web/build/bundle')
    into('build/resources/release')
}

processReleaseResources.dependsOn copyWebBundle
